<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%"
		 mouseEnabled="true"
		 mouseChildren="false"
		 buttonMode="true"
		 creationComplete="group1_creationCompleteHandler(event)"
		 rollOut="group1_rollOutHandler(event)"
		 rollOver="group1_rollOverHandler(event)"
		 click="group1_clickHandler(event)">
	<fx:Metadata>
		[Event(name="hermes",type="be.dragdesign.tchpc.events.HermesEvent")]
		[Event(name="hermes_reporting",type="be.dragdesign.tchpc.events.HermesEvent")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import be.dragdesign.tchpc.events.HermesEvent;
			
			import com.greensock.*;
			import com.greensock.easing.*;
			
			import mx.events.FlexEvent;
			
			//
			//	Fields
			//
			
			private var _imageSource:String;
			private var _onLock:Boolean = false
			
			//
			//	Getters & Setters
			//
			
			public function get imageSource():String
			{
				return _imageSource;
			}

			public function set imageSource(value:String):void
			{
				_imageSource = value;
				image_button.source = _imageSource;
			}

			public function get onLock():Boolean
			{
				return _onLock;
			}

			public function set onLock(value:Boolean):void
			{
				_onLock = value;
			}
			
			
			//
			//	Mouse Events ! 
			//
			
			protected function group1_rollOutHandler(event:MouseEvent):void
			{
				if(!onLock)
				{
					tween_unselected();
				}
			}


			protected function group1_rollOverHandler(event:MouseEvent):void
			{
				if(!onLock)
				{
					tween_selected();
				}
			}
			
			public function unlock():void
			{
				tween_unselected();
				this.onLock = false;
			}
			
			public function lock():void
			{				
				onLock = true
				tween_selected();
				dispatchEvent(new HermesEvent(HermesEvent.HERMES_REQUESTS_YOUR_ATTENTION,this.id));
			}

			protected function group1_clickHandler(event:MouseEvent):void
			{
				lock();				
			}
			
			//
			//	TWEENS
			//
			
			private function tween_selected():void
			{
				TweenMax.to(image_button, .4, {alpha:1,colorMatrixFilter:{contrast:1.25,amount:.8, brightness:1, saturation:1},glowFilter:{color:0xffffff, alpha:0.6, blurX:10, blurY:10, strength:1, quality:1},bevelFilter:{blurX:15, blurY:15, strength:.5, angle:45, distance:20}});
			}
			
			private function tween_unselected():void
			{
				TweenMax.to(image_button, .4, {alpha:.75,colorMatrixFilter:{colorize:0x333333,contrast:1.25, amount:.4, brightness:1, saturation:.6},glowFilter:{color:0xffffff, alpha:0, blurX:0, blurY:0, strength:0, quality:0},bevelFilter:{blurX:0, blurY:0, strength:0, angle:45, distance:0}});
			}

			protected function group1_creationCompleteHandler(event:FlexEvent):void
			{
				tween_unselected();
				dispatchEvent(new HermesEvent(HermesEvent.HERMES_REPORTS_FOR_DUTY,"REPORTING_FOR_DUTY"));
			}

		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:Image alpha=".75" verticalCenter="0" horizontalCenter="0" 
			  id="image_button" 
			  autoLoad="true" 
			  scaleContent="true"
			  smoothBitmapContent="true" />
</s:Group>
